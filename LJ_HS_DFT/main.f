	PROGRAM MAIN

!     THIS PROGRAM IS FOR THE LENNARD-JONES FLUID (WITH A CUT-OFF AT RC, NO SHIFT) AROUND A SPHERE
!	DFT: MFMT + FMSA (ZD LI AND WU, IECR, 2008)
!     BOUNDARIES: DENSITY PROFILE: [(DC-DPE)/2,RB] 
!      DC  = PARTICLE DIAMETER 
!      DPE = EFFECTIVE DIAMETER OF THE LJ SPHERE
!      RB  = UPPER COMPUTATIONAL BOUNDARY 
!      MAXIMUM NUMBER OF POINTS IN RHO: NP=8388608
	IMPLICIT NONE

!	 SEE THE INPUT FILE FOR THE DEFINITIONS
      REAL*8 RHOB,DC,DP,T,MIX_F,RB,RC,DR,TOLE,PI
      COMMON RHOB,DC,DP,T,MIX_F,RB,RC,DR,TOLE,PI

      INTEGER NP,ICODE
      PARAMETER(NP=2**13)

	REAL*8 DPE, DCP
	REAL*8 RHO(NP),RHO1(NP)
	REAL*8 N(3,NP),DDR(3,NP),DFR,DFATT,DFS
	REAL*8 AMU,LAMDA,DZ

	REAL*8 Z(2),X(2,7)
	REAL VCAVITY, SCAVITY, FSOL, RHOC
	
      INTEGER I,K,KIN,IR,NR
	
      INTEGER ITER,ACCU
	REAL*8 ERR,ACCERR

	REAL*8 GAMMA,RGPEX 
	REAL*8 MU,P,PAT,MUAT,GAMMA0,RGP0,RGP,TENSION
! FMSA PARAMETERS	
	COMMON /FMSA/ DPE,Z,X

! READ THE SYSTEM PARAMETERS
	CALL READDAT (ICODE)

! CALCULATE THE FMSA PARAMETERS
	CALL CRATT

! BULK PRESSURE AND CHEMICAL POTENTIAL
	CALL CP(T,RHOB,DP,DPE,RC,MU,MUAT)
	CALL PRESSURE(T,RHOB,DP,DPE,RC,P,PAT)

! MINIMUM DISTANCE FOR A NON-ZERO DENSITY
	DCP=(DC+DP)/2.0

! COMPUTATIONAL BOUNDARIES FOR THE DENSITY PROFILE  
 	IR=NINT((-DP)/2./DR)+1
	NR=IR+NINT(RB/DR)

!     INITIALIZE THE INHOMOGENEOUS DENSITY PROFILE	
	IF(ICODE.EQ.0) THEN
      DO K=IR,NR
	I=K-IR+1
	DZ=FLOAT(K-1)*DR
		IF(DZ.LT.DP/2.) THEN
			RHO(I)=0.0
		ELSE
			RHO(I)=RHOB
	    ENDIF
      ENDDO

	ELSE
!      READ THE INITIAL DENSITY PROFILE FROM A FILE
      OPEN(2,FILE='TEMPORY')
	DO K=IR,NR
	I=K-IR+1
	READ(2,*) DZ,RHO(I)
      ENDDO
      CLOSE(2)
	ENDIF

!     SET THE BULK DENSITY, RHO IS NOT DEFINED BEYOND NP! 
      DO I=NR-IR+2,NP
	RHO(I)=RHOB
      ENDDO

!     START PICARD ITERATION 
	ITER=0

! CALCULATE A MODIFIED CHEMICAL POTENTIAL FOR DFT CALCULATION      
      CALL CPM(AMU)

!     CALCULATE THE DERIVATIVES OF THE FREE ENERGY DENSITY 
!       WITH RESPECT TO THE WEIGHTED DENSITIES IN FMT
1     CALL DREP(IR,NR,DPE,RHO,N,DDR)

      DO K=IR,NR
	I=K-IR+1
	DZ=FLOAT(K-1)*DR
		IF(DZ.LT.DP/2.) THEN
			RHO1(I)=0.0
		ELSE
			CALL DFEXR(K,IR,DPE,DDR,DFR)
			CALL DFEXATT(K,IR,NR,RHO,DFATT)
			CALL DFEXS(K,IR,NR,RHO,DFS)
			LAMDA=DFR+DFATT+DFS-AMU
			RHO1(I)=EXP(-LAMDA)
		ENDIF
	ENDDO  

! save intermediate results
	OPEN(3,FILE='TEMPORY')
	DO K=IR,NR
	I=K-IR+1
	DZ=FLOAT(K-1)*DR
	WRITE(3,2) DZ,RHO1(I)
	ENDDO
	CLOSE(3)

!     CONVERGENCE CRITERIA
      ACCU=0
	ERR=0.0
	ACCERR=0.0      

	DO K=IR,NR
	I=K-IR+1
	IF (ABS(RHO1(I)-RHO(I))/RHOB.GE.TOLE) THEN
C        write(*,*) i,float(k-1)*dr,rho1(i),rho(i)
	ACCU=ACCU+1
	ENDIF
	IF (ERR.LT.ABS(RHO1(I)-RHO(I))/RHOB) THEN
	ERR=ABS(RHO1(I)-RHO(I))/RHOB
	ENDIF
	ACCERR=ACCERR+ABS(RHO1(I)-RHO(I))/RHOB
      ENDDO

      IF (ACCU.GT.0) THEN
	ITER=ITER+1
	WRITE(*,*) ITER,ACCU,ERR,ACCERR

	DO K=IR,NR
	I=K-IR+1
	RHO(I)=(1.-MIX_F)*RHO(I)+MIX_F*RHO1(I)
	ENDDO

	GOTO 1
	ENDIF

! OUTPUT THE RESULTS            
	OPEN(55,FILE='profile')
	KIN=NINT(DP/2./DR)+1 ! THE FIRST NON-ZERO DENSITY POINT
	RHOC=RHO(KIN-IR+1)
	DO K=KIN,NR
	I=K-IR+1
	RHO(I)=NINT(RHO(I)/RHOB/TOLE)*RHOB*TOLE
	DZ=FLOAT(K-1)*DR
	WRITE(55,2) DZ,RHO(I)
	ENDDO
	CLOSE(55)

! CALCULATE THERMODYNAMIC PROPERTIES
	NR=IR+NINT((RB-0.5)/DR)

	CALL THERMO(IR,NR,RHO,GAMMA,RGP)

! CALCULATE THERMODYNAMIC PROPERTIES AT A UNIFORM DENSITY
      DO K=IR,NR
	I=K-IR+1
	DZ=FLOAT(K-1)*DR
		IF(DZ.LT.DP/2.) THEN
			RHO(I)=0.0
		ELSE
			RHO(I)=RHOB
	    ENDIF
      ENDDO

	CALL THERMO(IR,NR,RHO,GAMMA0,RGP0)

	VCAVITY=4.*PI*DCP**3/3.
	SCAVITY=4.*PI*DCP**2
	GAMMA=(GAMMA-GAMMA0)/SCAVITY  !SURFACE EXCESS
	RGPEX=(RGP-RGP0)  !EXCESS GRAND POTENTIAL
	TENSION=RGPEX/SCAVITY  !SOLID-LIQUID TENSION
	FSOL=P*VCAVITY+RGPEX*T   !PER EPISLON

	OPEN(66,FILE='output')
	WRITE(66,*) '  BULK TEMPERATURE AND DENSITY:'
	WRITE(66,2) T,RHOB
	WRITE(66,*) '  BULK PRESSURE AND CHEMICAL POTENTIAL:'
	WRITE(66,2) P,MU
	WRITE(66,*) '  CUT-OFF DISTANCE AND PARTICLE RADIUS'
	WRITE(66,3) RC,DC/2
	WRITE(66,*) '  SURFACE EXCESS AND TENSION:'
	WRITE(66,2) GAMMA,TENSION
	WRITE(66,*) '  CONTACT DENSITY AND SOLVATION FREE ENERGY:'
	WRITE(66,3) RHOC,FSOL
	CLOSE(66)

2     FORMAT(3X,F12.6,3X,F12.6)
3     FORMAT(3X,F12.6,3X,E12.6)
      STOP
	END
